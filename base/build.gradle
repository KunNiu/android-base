apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

ext {
    moduleName = 'SKTLab Android Base Library'
    moduleGroupId = 'com.sktlab.android'
    moduleArtifactId = 'base'
    moduleVersion = '1.0.1'
    moduleVersionCode = 101
    moduleDescription = 'SKTLab Base Library for Android'
    moduleUrl = 'https://github.com/SKTLab/android-base'
    moduleDeveloperId = 'KunNiu'
    moduleDeveloperName = 'Nick Niu'
    moduleDeveloperEmail = 'kun.niu@sktlab.com'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

publishing {
    def repo_username = project.properties.BintrayUserName
    def repo_password = project.properties.BintrayApiKey
    publications {
        maven(MavenPublication) {
            groupId = moduleGroupId
            artifactId = moduleArtifactId
            version = moduleVersion
            artifact sourcesJar
            artifact "$buildDir/outputs/aar/base-debug.aar"

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    if (dependency.group?.trim()) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name moduleName
                    description moduleDescription
                    url moduleUrl
                    scm {
                        url moduleUrl
                        connection moduleUrl
                        developerConnection moduleUrl
                    }
                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id moduleDeveloperId
                            name moduleDeveloperName
                            email moduleDeveloperEmail
                        }
                    }
                }
            }
        }
    }
}

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    testImplementation deps.junit
    androidTestImplementation deps.android_test.ext
    androidTestImplementation deps.android_test.espresso

    implementation deps.appcompat
    implementation deps.fragment
    implementation deps.phonenumber
    implementation deps.recyclerview
    implementation deps.matisse
    implementation deps.apache_commons.lang
    implementation deps.apache_commons.validator
    implementation deps.eventbus
    implementation deps.butterknife.butterknife
    implementation deps.pinyin
    implementation deps.gson
    implementation deps.okhttp.okhttp
    implementation deps.okhttp.logging_interceptor
    implementation deps.glide.glide
    annotationProcessor deps.glide.compiler
    implementation deps.glide.transformations
    implementation deps.viewpager2
    implementation deps.multitype
}
